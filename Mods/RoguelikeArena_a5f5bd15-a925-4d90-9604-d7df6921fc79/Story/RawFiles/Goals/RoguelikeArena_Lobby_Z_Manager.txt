Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_RoguelikeArena_RegionTeleportTriggers(0, TRIGGERGUID_TeleportTrigger_050_e3dbd68b-b7e0-4903-bc11-5717f65ff068);

DB_RoguelikeArena_LobbyTeleporterItem((ITEMGUID)ITEMGUID_RoguelikeArena_Portal_000_ff11d147-61e9-44bc-a5f8-c106996b875d);
KBSECTION
IF
CharacterUsedItem(_Character, _UsedItem)
AND
DB_RoguelikeArena_LobbyTeleporterItem(_UsedItem)
THEN
PROC_RoguelikeArena_UseLobbyTeleporter(_Character);

// Progresses the arena.
PROC
PROC_RoguelikeArena_UseLobbyTeleporter((CHARACTERGUID)_Character)
AND
QRY_RoguelikeArena_IsCurrentChallengeComplete()
AND
QRY_RoguelikeArena_IsCurrentArenaClear()
THEN
PROC_RoguelikeArena_ProgressArena();

// Resets the active arena and generates a new one.
PROC
PROC_RoguelikeArena_UseLobbyTeleporter(_Character)
AND
QRY_RoguelikeArena_IsCurrentChallengeComplete()
AND
QRY_RoguelikeArena_IsCurrentArenaClear()
AND
DB_RoguelikeArena_Singleton_CurrentLevel(_CurrentLevel)
AND
DB_RoguelikeArena_Singleton_CurrentDifficulty(_CurrentDifficulty)
THEN
PROC_RoguelikeArena_ResetActiveArena();
PROC_RoguelikeArena_StartNewArena(_CurrentLevel, _CurrentDifficulty);

PROC
PROC_RoguelikeArena_UseLobbyTeleporter(_Character)
AND
DB_RoguelikeArena_ActiveArena(_Arena, _, _)
AND
DB_RoguelikeArena_RegionTeleportTriggers(_Arena, _Trigger)
THEN
TeleportTo(_Character, _Trigger, "", 1);


// Prevents the use of the teleporter in combat.
PROC
ProcBlockUseOfItem(_Player, _Item)
AND
DB_RoguelikeArena_LobbyTeleporterItem(_Item)
AND
DB_IsPlayer(_Player)
AND
DB_CombatCharacters(_Player, _)
THEN
DB_CustomUseItemResponse(_Player, _Item, 0);
Proc_StartDialog(1, "GLO_AD_CannotUseNow", _Player);
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "RoguelikeArena_Lobby"
