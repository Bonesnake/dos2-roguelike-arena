Version 1
SubGoalCombiner SGC_AND
INITSECTION
// Min, max, min increase per difficulty, max increase per difficulty.
DB_RoguelikeArena_ContainerCountParameters(3, 5, 2, 3);

DB_RoguelikeArena_ContainerRankRanges(0, 0, 0, 30);
DB_RoguelikeArena_ContainerRankRanges(0, 1, 30, 55);
DB_RoguelikeArena_ContainerRankRanges(0, 2, 55, 75);
DB_RoguelikeArena_ContainerRankRanges(0, 3, 75, 90);
DB_RoguelikeArena_ContainerRankRanges(0, 4, 90, 100);

DB_RoguelikeArena_ContainerRankRanges(1, 0, 0, 25);
DB_RoguelikeArena_ContainerRankRanges(1, 1, 25, 50);
DB_RoguelikeArena_ContainerRankRanges(1, 2, 50, 70);
DB_RoguelikeArena_ContainerRankRanges(1, 3, 70, 85);
DB_RoguelikeArena_ContainerRankRanges(1, 4, 85, 100);

DB_RoguelikeArena_ContainerRankRanges(2, 0, 0, 20);
DB_RoguelikeArena_ContainerRankRanges(2, 1, 20, 40);
DB_RoguelikeArena_ContainerRankRanges(2, 2, 40, 60);
DB_RoguelikeArena_ContainerRankRanges(2, 3, 60, 80);
DB_RoguelikeArena_ContainerRankRanges(2, 4, 80, 100);

DB_RoguelikeArena_ContainerRankRanges(3, 0, 0, 15);
DB_RoguelikeArena_ContainerRankRanges(3, 1, 15, 30);
DB_RoguelikeArena_ContainerRankRanges(3, 2, 30, 50);
DB_RoguelikeArena_ContainerRankRanges(3, 3, 50, 75);
DB_RoguelikeArena_ContainerRankRanges(3, 4, 75, 100);

DB_RoguelikeArena_ContainerRankRanges(4, 0, 0, 10);
DB_RoguelikeArena_ContainerRankRanges(4, 1, 10, 25);
DB_RoguelikeArena_ContainerRankRanges(4, 2, 25, 45);
DB_RoguelikeArena_ContainerRankRanges(4, 3, 45, 70);
DB_RoguelikeArena_ContainerRankRanges(4, 4, 70, 100);

DB_RoguelikeArena_ContainerTemplates(0, "RoguelikeArena_Container_Rank1_9bdb25d3-8168-4a37-afab-7dd804555e18", "RoguelikeArena_Container_Rank1");
DB_RoguelikeArena_ContainerTemplates(1, "RoguelikeArena_Container_Rank2_9000b256-c3a7-4cc7-9294-e6d085397ed2", "RoguelikeArena_Container_Rank2");
DB_RoguelikeArena_ContainerTemplates(2, "RoguelikeArena_Container_Rank3_9dd4cb0c-9d53-4ba1-91db-6fcd6ad10f26", "RoguelikeArena_Container_Rank3");
DB_RoguelikeArena_ContainerTemplates(3, "RoguelikeArena_Container_Rank4_9709290c-20a1-4f9d-a56b-c97d9a2a6cff", "RoguelikeArena_Container_Rank4");
DB_RoguelikeArena_ContainerTemplates(4, "RoguelikeArena_Container_Rank5_48bfa64c-6b53-4750-bf2b-b1c8353fb8f2", "RoguelikeArena_Container_Rank5");
KBSECTION
// Calculates the amount of containers to be spawned, then spawns that amount.
PROC
PROC_RoguelikeArena_SpawnContainers((INTEGER)_Arena, (INTEGER)_Level, (INTEGER)_Difficulty)
AND
DB_RoguelikeArena_ContainerCountParameters(_ContainerCountMin, _ContainerCountMax, _ContainerCountMinIncreasePerDifficulty, _ContainerCountMaxIncreasePerDifficulty)
AND
IntegerProduct(_Difficulty, _ContainerCountMinIncreasePerDifficulty, _ContainerCountMinModifier)
AND
IntegerProduct(_Difficulty, _ContainerCountMaxIncreasePerDifficulty, _ContainerCountMaxModifier)
AND
IntegerSum(_ContainerCountMin, _ContainerCountMinModifier, _ContainerCountMinModified)
AND
IntegerSum(_ContainerCountMax, _ContainerCountMaxModifier, _ContainerCountMaxModified)
AND
QRY_RoguelikeArena_RandomInclusive(_ContainerCountMinModified, _ContainerCountMaxModified)
AND
DB_RoguelikeArena_RandomResult(_ContainerCount)
THEN
PROC_RoguelikeArena_SpawnContainers(_Arena, _Level, _Difficulty, _ContainerCount);

// Spawns the containers.
PROC
PROC_RoguelikeArena_SpawnContainers((INTEGER)_Arena, (INTEGER)_Level, (INTEGER)_Difficulty, (INTEGER)_Count)
AND
QRY_RoguelikeArena_CreateIndices(_Count)
AND
DB_RoguelikeArena_CreatedIndices(_)
THEN
PROC_RoguelikeArena_SpawnContainer(_Arena, _Level, _Difficulty);

PROC
PROC_RoguelikeArena_SpawnContainer((INTEGER)_Arena, (INTEGER)_Level, (INTEGER)_Difficulty)
AND
Random(99, _Random)
AND
DB_RoguelikeArena_ContainerRankRanges(_Difficulty, _Rank, _Min, _Max)
AND
_Random >= _Min
AND
_Random < _Max
AND
DB_RoguelikeArena_ContainerTemplates(_Rank, _Template, _Treasure)
AND
QRY_RoguelikeArena_GetRandomPositionInArena(_Arena, "SPAWNER_EVENT")
AND
DB_RoguelikeArena_RandomPositionResult(_X, _Y, _Z)
AND
CreateItemTemplateAtPosition(_Template, _X, _Y, _Z, _Container)
THEN
PROC_RoguelikeArena_ItemRandomRotate(_Container);
// PROC_RoguelikeArena_ItemTeleportToValidPosition(_Container);
// PROC_RoguelikeArena_ItemSnapToGround(_Container);
ItemLevelUpTo(_Container, _Level);
GenerateTreasure(_Container, _Treasure, _Level, NULL_00000000-0000-0000-0000-000000000000);
PROC_RoguelikeArena_RegisterForCleanup(_Container, _Arena);
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "RoguelikeArena_Arena"
