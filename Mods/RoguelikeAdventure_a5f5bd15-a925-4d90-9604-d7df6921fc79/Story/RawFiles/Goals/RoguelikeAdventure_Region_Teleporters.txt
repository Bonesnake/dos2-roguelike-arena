Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_RoguelikeAdventure_RegionTeleporterTemplate("RoguelikeAdventure_Portal_b3fa8b47-a181-40e5-b33b-fdc92db7bec9");

// Direction, rotation, -X, -Z, +X, +Z
DB_RoguelikeAdventure_RegionTeleporterTransforms("west", 270.0, 4.0, 0.0, 0.0, 0.0);
DB_RoguelikeAdventure_RegionTeleporterTransforms("north", 0.0, 0.0, 0.0, 0.0, 4.0);
DB_RoguelikeAdventure_RegionTeleporterTransforms("east", 90.0, 0.0, 0.0, 4.0, 0.0);
DB_RoguelikeAdventure_RegionTeleporterTransforms("south", 180.0, 0.0, 4.0, 0.0, 0.0);
KBSECTION
IF
DB_RoguelikeAdventure_ActiveRegions(_Region, _Level, _Index)
THEN
PROC_RoguelikeAdventure_SpawnRegionTeleporters(_Region, _Level, _Index);

// Populates a region with teleporters.
PROC
PROC_RoguelikeAdventure_SpawnRegionTeleporters((INTEGER)_Region, (INTEGER)_Level, (INTEGER)_Index)
AND
DB_RoguelikeAdventure_SectionExitStates(_Region, _Section, _Trigger, _IsForward)
AND
DB_RoguelikeAdventure_SectionExits(_Region, _Section, (TRIGGERGUID)_Trigger, _Direction)
AND
GetPosition(_Trigger, _X, _Y, _Z)
AND
DB_RoguelikeAdventure_RegionTeleporterTransforms(_Direction, _Angle, _XSubtract, _ZSubtract, _XAdd, _ZAdd)
AND
RealSubtract(_X, _XSubtract, _XSubtracted)
AND
RealSubtract(_Z, _ZSubtract, _ZSubtracted)
AND
RealSum(_XSubtracted, _XAdd, _XTransformed)
AND
RealSum(_ZSubtracted, _ZAdd, _ZTransformed)
AND
DB_RoguelikeAdventure_RegionTeleporterTemplate(_Template)
AND
CreateItemTemplateAtPosition(_Template, _XTransformed, _Y, _ZTransformed, _Teleporter)
THEN
ItemRotateToAngleY(_Teleporter, _Angle, 10000.0);
PROC_RoguelikeAdventure_ItemSnapToGround(_Teleporter);
DB_RoguelikeAdventure_RegionTeleporters(_Region, _Index, _Teleporter, (TRIGGERGUID)_Trigger, _IsForward);
PROC_RoguelikeAdventure_RegisterForCleanup(_Teleporter, _Region);

// Registers backwards teleport items and triggers.
IF
DB_RoguelikeAdventure_RegionTeleporters(_Region, _Index, _Teleporter, _Trigger, 0)
AND
IntegerProduct(_Index, 2, _IndexDoubled)
AND
IntegerSubtract(_IndexDoubled, 2, _TeleporterId)
AND
IntegerSubtract(_IndexDoubled, 1, _TriggerId)
THEN
PROC_RoguelikeAdventure_RegisterTeleporterItem(_Teleporter, _TeleporterId);
PROC_RoguelikeAdventure_RegisterTeleporterTrigger(_Trigger, _TriggerId);

// Registers forward teleport items and triggers.
IF
DB_RoguelikeAdventure_RegionTeleporters(_Region, _Index, _Teleporter, _Trigger, 1)
AND
DB_RoguelikeAdventure_Singleton_RegionsPerLevel(_RegionsPerLevel)
AND
_Index < _RegionsPerLevel
AND
IntegerProduct(_Index, 2, _IndexDoubled)
AND
IntegerSum(_IndexDoubled, 1, _TeleporterId)
THEN
PROC_RoguelikeAdventure_RegisterTeleporterItem(_Teleporter, _TeleporterId);
PROC_RoguelikeAdventure_RegisterTeleporterTrigger(_Trigger, _IndexDoubled);

// Registers forward teleport items and triggers.
IF
DB_RoguelikeAdventure_RegionTeleporters(_Region, _Index, _Teleporter, _Trigger, 1)
AND
DB_RoguelikeAdventure_Singleton_RegionsPerLevel(_RegionsPerLevel)
AND
_Index >= _RegionsPerLevel
THEN
PROC_RoguelikeAdventure_RegisterTeleporterItem(_Teleporter, 0);

// Prepares the next region if needed.
IF
CharacterUsedItem(_Character, _Item)
AND
DB_RoguelikeAdventure_RegionTeleporters(_, _Index, _Item, _, 1)
AND
DB_RoguelikeAdventure_Singleton_RegionsPerLevel(_RegionsPerLevel)
AND
_Index < _RegionsPerLevel
AND
IntegerSum(_Index, 1, _NextIndex)
AND
NOT DB_RoguelikeAdventure_ActiveRegions(_, _, _NextIndex)
AND
DB_RoguelikeAdventure_Singleton_CurrentLevel(_Level)
THEN
PROC_RoguelikeAdventure_StartNewRegion(_Level);

// Removes teleporter from DB after removed from the game.
IF
DB_RoguelikeAdventure_RemovedItems(_Item)
AND
DB_RoguelikeAdventure_RegionTeleporters(_Region, _Index, _Item, _Trigger, _IsForward)
THEN
NOT DB_RoguelikeAdventure_RegionTeleporters(_Region, _Index, _Item, _Trigger, _IsForward);
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "RoguelikeAdventure_Region"