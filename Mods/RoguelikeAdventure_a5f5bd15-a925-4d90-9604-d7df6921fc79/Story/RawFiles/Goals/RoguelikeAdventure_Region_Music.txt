Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_RoguelikeAdventure_GlobalMusicHelperOutside(ITEMGUID_MusicHelper_Outside_9b049514-edc0-4c94-8544-ed4d6c1fe42c);

DB_RoguelikeAdventure_MusicTriggers(TRIGGERGUID_Music01_3e009c68-8cfc-4c07-b016-2324ce591b8c);
DB_RoguelikeAdventure_MusicTriggers(TRIGGERGUID_Music02_23dacc8e-c274-49df-898e-2618737ef2dc);
DB_RoguelikeAdventure_MusicTriggers(TRIGGERGUID_Music03_824fd5cd-ed63-48f9-afe4-3383f1fd48bc);
DB_RoguelikeAdventure_MusicTriggers(TRIGGERGUID_Music04_edf644e9-ccbb-45ae-bf2e-030d1e3b0f8c);
DB_RoguelikeAdventure_MusicTriggers(TRIGGERGUID_Music05_992ac2db-0ef4-46e3-b95a-40cc935be2d0);

DB_RoguelikeAdventure_BossMusicTriggers(TRIGGERGUID_BossMusic01_58190241-8aae-4746-9f83-c738111e7204);
DB_RoguelikeAdventure_BossMusicTriggers(TRIGGERGUID_BossMusic02_e26f6263-a4d3-49d6-9c2c-0b9d0fbf2244);
DB_RoguelikeAdventure_BossMusicTriggers(TRIGGERGUID_BossMusic03_c068ebcc-4fa2-49e5-b57c-f66560fdda3a);

PROC_RoguelikeAdventure_GuidStringAddMusicTriggers();
PROC_RoguelikeAdventure_GuidStringAddBossMusicTriggers();
KBSECTION
// Restores music triggers after loading a save. This is necessary, because music triggers' position is not saved.
IF
SavegameLoaded(_, _, _, _)
AND
DB_RoguelikeAdventure_ActiveMusicTriggers(_Region, _Trigger)
THEN
PROC_RoguelikeAdventure_MoveInMusicTrigger(_Region, _Trigger);

IF
DB_RoguelikeAdventure_ActiveRegions(_Region, _, _Index)
THEN
PROC_RoguelikeAdventure_PlayRandomMusic(_Region, _Index);

IF
DB_RoguelikeAdventure_ActiveRegionsHelper(_Region, _Level, _Index)
AND
NOT DB_RoguelikeAdventure_ActiveRegions(_Region, _Level, _Index)
THEN
PROC_RoguelikeAdventure_StopMusic(_Region);

// Plays a random music according to the current progression.
PROC
PROC_RoguelikeAdventure_PlayRandomMusic((INTEGER)_Region, (INTEGER)_Index)
AND
DB_RoguelikeAdventure_Singleton_RegionsPerLevel(_RegionsPerLevel)
AND
_Index < _RegionsPerLevel
AND
QRY_RoguelikeAdventure_GuidStringPoolSelectAndRemoveRandom("MusicTriggerPool")
AND
DB_RoguelikeAdventure_GuidStringPoolSelectedValue("MusicTriggerPool", _Trigger)
THEN
PROC_RoguelikeAdventure_MoveInMusicTrigger(_Region, _Trigger);

// Plays a random music according to the current progression.
PROC
PROC_RoguelikeAdventure_PlayRandomMusic(_Region, _Index)
AND
DB_RoguelikeAdventure_Singleton_RegionsPerLevel(_RegionsPerLevel)
AND
_Index >= _RegionsPerLevel
AND
QRY_RoguelikeAdventure_GuidStringPoolSelectAndRemoveRandom("BossMusicTriggerPool")
AND
DB_RoguelikeAdventure_GuidStringPoolSelectedValue("BossMusicTriggerPool", _Trigger)
THEN
PROC_RoguelikeAdventure_MoveInMusicTrigger(_Region, _Trigger);

// Removes music trigger from the region.
PROC
PROC_RoguelikeAdventure_StopMusic((INTEGER)_Region)
AND
DB_RoguelikeAdventure_ActiveMusicTriggers(_Region, _Trigger)
AND
DB_RoguelikeAdventure_MusicTriggers(_Trigger)
THEN
PROC_RoguelikeAdventure_GuidStringPoolAdd("MusicTriggerPool", _Trigger);
PROC_RoguelikeAdventure_MoveOutMusicTrigger(_Trigger);

// Removes music trigger from the region.
PROC
PROC_RoguelikeAdventure_StopMusic((INTEGER)_Region)
AND
DB_RoguelikeAdventure_ActiveMusicTriggers(_Region, _Trigger)
AND
DB_RoguelikeAdventure_BossMusicTriggers(_Trigger)
THEN
PROC_RoguelikeAdventure_GuidStringPoolAdd("BossMusicTriggerPool", _Trigger);
PROC_RoguelikeAdventure_MoveOutMusicTrigger(_Trigger);

// Moves in a music trigger to the given region and stores it in a DB.
PROC
PROC_RoguelikeAdventure_MoveInMusicTrigger((INTEGER)_Region, (GUIDSTRING)_Trigger)
AND
DB_RoguelikeAdventure_Regions(_Region, _Target)
THEN
TeleportTo(_Trigger, _Target, "", 0);

// Moves in a music trigger to the given region and stores it in a DB.
PROC
PROC_RoguelikeAdventure_MoveInMusicTrigger((INTEGER)_Region, (GUIDSTRING)_Trigger)
AND
NOT DB_RoguelikeAdventure_ActiveMusicTriggers(_Region, _Trigger)
THEN
DB_RoguelikeAdventure_ActiveMusicTriggers(_Region, _Trigger);

// Moves out a music trigger and removes it from the DB.
PROC
PROC_RoguelikeAdventure_MoveOutMusicTrigger((GUIDSTRING)_Trigger)
AND
DB_RoguelikeAdventure_GlobalMusicHelperOutside(_Target)
THEN
TeleportTo(_Trigger, _Target, "", 0);

// Moves out a music trigger and removes it from the DB.
PROC
PROC_RoguelikeAdventure_MoveOutMusicTrigger((GUIDSTRING)_Trigger)
AND
DB_RoguelikeAdventure_ActiveMusicTriggers(_Region, _Trigger)
THEN
NOT DB_RoguelikeAdventure_ActiveMusicTriggers(_Region, _Trigger);

PROC
PROC_RoguelikeAdventure_GuidStringAddMusicTriggers()
AND
DB_RoguelikeAdventure_MusicTriggers(_Trigger)
THEN
PROC_RoguelikeAdventure_GuidStringPoolAdd("MusicTriggerPool", _Trigger);

PROC
PROC_RoguelikeAdventure_GuidStringAddBossMusicTriggers()
AND
DB_RoguelikeAdventure_BossMusicTriggers(_Trigger)
THEN
PROC_RoguelikeAdventure_GuidStringPoolAdd("BossMusicTriggerPool", _Trigger);
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "RoguelikeAdventure_Region"