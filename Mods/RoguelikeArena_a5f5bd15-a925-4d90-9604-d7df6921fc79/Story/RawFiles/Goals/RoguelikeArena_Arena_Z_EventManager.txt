Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_RoguelikeArena_Events("BlessedStatueEvent");
DB_RoguelikeArena_Events("HazardEvent");
DB_RoguelikeArena_Events("MeteorShowerEvent");
DB_RoguelikeArena_Events("SourceBeaconEvent");
DB_RoguelikeArena_Events("SourceScarecrowEvent");
DB_RoguelikeArena_Events("SurfaceEvent");
KBSECTION
IF
DB_RoguelikeArena_ActiveArena(_Arena, _Level, _Difficulty, _)
THEN
PROC_RoguelikeArena_StartRandomEvents(_Arena, _Level, _Difficulty);

PROC
PROC_RoguelikeArena_StartRandomEvents((INTEGER)_Arena, (INTEGER)_Level, (INTEGER)_Difficulty)
AND
QRY_RoguelikeArena_RandomCount(_Difficulty, 1, 4, 1)
AND
DB_RoguelikeArena_RandomResult(_EventCount)
THEN
PROC_RoguelikeArena_StartRandomEvents(_Arena, _Level, _Difficulty, _EventCount);

PROC
PROC_RoguelikeArena_StartRandomEvents((INTEGER)_Arena, (INTEGER)_Level, (INTEGER)_Difficulty, (INTEGER)_Count)
THEN
PROC_RoguelikeArena_StringPoolClear("EventPool");
PROC_RoguelikeArena_StringPoolAddEvents();

PROC
PROC_RoguelikeArena_StartRandomEvents(_Arena, _Level, _Difficulty, _Count)
AND
QRY_RoguelikeArena_CreateIndices(_Count)
AND
DB_RoguelikeArena_CreatedIndices(_)
THEN
PROC_RoguelikeArena_StartRandomEvent(_Arena, _Level, _Difficulty);

PROC
PROC_RoguelikeArena_StartRandomEvent((INTEGER)_Arena, (INTEGER)_Level, (INTEGER)_Difficulty)
AND
QRY_RoguelikeArena_StringPoolSelectAndRemoveRandom("EventPool")
AND
DB_RoguelikeArena_StringPoolSelectedValue("EventPool", _Event)
THEN
PROC_RoguelikeArena_StartEvent(_Arena, _Level, _Difficulty, _Event);

PROC
PROC_RoguelikeArena_StartEvent((INTEGER)_Arena, (INTEGER)_Level, (INTEGER)_Difficulty, (STRING)_Event)
AND
_Event == "BlessedStatueEvent"
THEN
PROC_RoguelikeArena_SpawnBlessedStatues(_Arena, _Level, _Difficulty);

PROC
PROC_RoguelikeArena_StartEvent(_Arena, _Level, _Difficulty, _Event)
AND
_Event == "HazardEvent"
THEN
PROC_RoguelikeArena_SpawnHazards(_Arena, _Level, _Difficulty);

PROC
PROC_RoguelikeArena_StartEvent(_Arena, _Level, _Difficulty, _Event)
AND
_Event == "MeteorShowerEvent"
THEN
PROC_RoguelikeArena_SpawnMeteorShowers(_Arena, _Level, _Difficulty);

PROC
PROC_RoguelikeArena_StartEvent(_Arena, _Level, _Difficulty, _Event)
AND
_Event == "SourceBeaconEvent"
THEN
PROC_RoguelikeArena_SpawnSourceBeacons(_Arena, _Level, _Difficulty);

PROC
PROC_RoguelikeArena_StartEvent(_Arena, _Level, _Difficulty, _Event)
AND
_Event == "SourceScarecrowEvent"
THEN
PROC_RoguelikeArena_SpawnSourceScarecrows(_Arena, _Level, _Difficulty);

PROC
PROC_RoguelikeArena_StartEvent(_Arena, _Level, _Difficulty, _Event)
AND
_Event == "SurfaceEvent"
THEN
PROC_RoguelikeArena_CreateSurfaces(_Arena, _Level, _Difficulty);

PROC
PROC_RoguelikeArena_StringPoolAddEvents()
AND
DB_RoguelikeArena_Events(_Event)
THEN
PROC_RoguelikeArena_StringPoolAdd("EventPool", _Event);
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "RoguelikeArena_Arena_Event"
