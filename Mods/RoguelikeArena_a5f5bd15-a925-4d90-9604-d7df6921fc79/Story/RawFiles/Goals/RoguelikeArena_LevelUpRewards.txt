Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION
// Increases available source points to 2.
IF
CharacterLeveledUp(_Character)
AND
QRY_RoguelikeArena_ClaimLevelUpRewardIfAvailable(_Character, 4, "SecondSourcePoint")
AND
CharacterGetMaxSourcePoints(_Character, _CurrentMax)
AND
_CurrentMax < 2
THEN
CharacterOverrideMaxSourcePoints(_Character, 2);

// Increases available source points to 3.
IF
CharacterLeveledUp(_Character)
AND
QRY_RoguelikeArena_ClaimLevelUpRewardIfAvailable(_Character, 9, "ThirdSourcePoint")
AND
CharacterGetMaxSourcePoints(_Character, _CurrentMax)
AND
_CurrentMax < 3
THEN
CharacterOverrideMaxSourcePoints(_Character, 3);

// Adds source vampirism.
IF
CharacterLeveledUp(_Character)
AND
QRY_RoguelikeArena_ClaimLevelUpRewardIfAvailable(_Character, 2, "SourceVampirism")
AND
CharacterHasSkill(_Character, "Target_SourceVampirism", 0)
THEN
CharacterAddSkill(_Character, "Target_SourceVampirism", 1);

// Adds bless.
IF
CharacterLeveledUp(_Character)
AND
QRY_RoguelikeArena_ClaimLevelUpRewardIfAvailable(_Character, 3, "Bless")
AND
CharacterHasSkill(_Character, "Target_Bless", 0)
THEN
CharacterAddSkill(_Character, "Target_Bless", 1);

// Adds curse.
IF
CharacterLeveledUp(_Character)
AND
QRY_RoguelikeArena_ClaimLevelUpRewardIfAvailable(_Character, 4, "Curse")
AND
CharacterHasSkill(_Character, "Target_Curse", 0)
THEN
CharacterAddSkill(_Character, "Target_Curse", 1);

QRY
QRY_RoguelikeArena_ClaimLevelUpRewardIfAvailable((CHARACTERGUID)_Character, (INTEGER)_Level, (STRING)_Reward)
AND
DB_IsPlayer(_Character)
AND
CharacterGetLevel(_Character, _CharacterLevel)
AND
_CharacterLevel >= _Level
AND
NOT DB_RoguelikeArena_LevelUpRewardIsClaimed(_Character, _Reward)
THEN
DB_RoguelikeArena_LevelUpRewardIsClaimed(_Character, _Reward);
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "RoguelikeArena"
