Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_RoguelikeArena_LobbyExtraFeatures("RoguelikeArena_DebugStatue", 0, 0, 0, ITEMGUID_BLD_Humans_Statue_Knight_A_Dynamic_001_c2045934-4a0b-47a5-8db1-90f7f965d771);
DB_RoguelikeArena_LobbyExtraFeatures("RoguelikeArena_EquipmentStatue", 2, 100, 25, (ITEMGUID)ITEMGUID_RoguelikeArena_Lobby_EquipmentStatue_000_f0b00fdf-5679-4b32-af98-32f84af287f2);
DB_RoguelikeArena_LobbyExtraFeatures("RoguelikeArena_ChestOfFortune", 3, 5, 5, ITEMGUID_CONT_Humans_Citz_Chest_Rich_A_001_b253200a-1651-43fd-9027-44a7834b7958);
DB_RoguelikeArena_LobbyExtraFeatures("RoguelikeArena_RecruitmentBoard", 1, 0, 0, ITEMGUID_TOOL_SignWall_A_Wood_001_ae04ac4e-72d8-4247-b82c-5bd82e98262f);
DB_RoguelikeArena_LobbyExtraFeatures("RoguelikeArena_LobbyPortal", 1, 0, 0, ITEMGUID_RoguelikeArena_Portal_000_ff11d147-61e9-44bc-a5f8-c106996b875d);
DB_RoguelikeArena_LobbyExtraFeatures("RoguelikeArena_Scoreboard", 1, 0, 0, ITEMGUID_TOOL_SignRoad_A_000_c3bb4701-393c-4b2e-8140-10f9ea791752);
DB_RoguelikeArena_LobbyExtraFeatures("RoguelikeArena_ArenaMutator", 4, 0, 0, ITEMGUID_FUR_Raanaar_Desk_B_000_86f3a40c-6ed8-4cea-b612-aa0e9bde8ed7);
DB_RoguelikeArena_LobbyExtraFeatures("RoguelikeArena_TrainingDummy", 1, 0, 0, ITEMGUID_FUR_Humans_TrainingDummy_D_001_41007f4d-4477-4dae-8a3b-7b44dd89aa8a);
DB_RoguelikeArena_LobbyExtraFeatures("RoguelikeArena_Entrance", 21, 0, 0, ITEMGUID_DOORS_WorkerHouseWood_A_Door_B_000_b6c07fc2-7f5e-4c81-8274-2114955b5151);

PROC_RoguelikeArena_InitLobbyExtraFeatures();
KBSECTION
// Creates quest states.
PROC
PROC_RoguelikeArena_InitLobbyExtraFeatures()
AND
DB_RoguelikeArena_LobbyExtraFeatures(_FeatureName, _, _, _, _)
THEN
DB_QuestDef_State(_FeatureName, "FeatureAvailable", 1);

// Adds initial extra feature journal entries.
PROC
PROC_RoguelikeArena_InitLobbyExtraFeatures()
AND
DB_IsPlayer(_Player)
THEN
PROC_RoguelikeArena_UpdateExtraFeaturesJournal(_Player);

// Initiates dialog.
IF
CharacterUsedItem(_Character, _Item)
AND
DB_RoguelikeArena_LobbyExtraFeatures(_FeatureName, _LevelMin, _PriceBase, _PriceModifier, _Item)
AND
CharacterGetLevel(_Character, _Level)
AND
_Level >= _LevelMin
AND
QRY_RoguelikeArena_Price(_Level, _PriceBase, _PriceModifier)
AND
DB_Singleton("RoguelikeArena_PriceResult", _Price)
THEN
Proc_StartDialog(0, _FeatureName, _Item, _Character);
DB_DialogMoneyTransfer(1, _FeatureName, _Price);

// Tells level requirement if level is not high enough.
IF
CharacterUsedItem(_Character, _Item)
AND
DB_RoguelikeArena_LobbyExtraFeatures(_, _LevelMin, _, _, _Item)
AND
CharacterGetLevel(_Character, _Level)
AND
_Level < _LevelMin
THEN
Proc_StartDialog(0, "RoguelikeArena_HigherLevelRequired", _Item, _Character);
DialogSetVariableInt("RoguelikeArena_HigherLevelRequired", "RoguelikeArena_LevelRequirement_ac29c66c-8679-41d2-944d-c4bcbff5fbfb", _LevelMin);

// Clears used flag after a feature was used.
IF
ObjectFlagSet(_Flag, _Speaker, _)
AND
DB_RoguelikeArena_LobbyExtraFeatures(_FeatureName, _, _, _, _)
AND
StringConcatenate(_FeatureName, "_WasUsed", _FlagExpected)
AND
_Flag == _FlagExpected
THEN
ObjectClearFlag(_Speaker, _Flag);

// Updates extra feature journal entries upon level up.
IF
CharacterLeveledUp(_Character)
THEN
PROC_RoguelikeArena_UpdateExtraFeaturesJournal(_Character);

// Updates extra feature journal entries.
PROC
PROC_RoguelikeArena_UpdateExtraFeaturesJournal((CHARACTERGUID)_Player)
AND
DB_RoguelikeArena_LobbyExtraFeatures(_FeatureName, _LevelMin, _, _, _)
AND
StringConcatenate("QuestUpdate_", _FeatureName, _FlagPart)
AND
StringConcatenate(_FlagPart, "_FeatureAvailable", _Flag)
AND
PartyGetFlag(_Player, _Flag, 0)
AND
CharacterGetLevel(_Player, _Level)
AND
_Level >= _LevelMin
THEN
PartySetFlag(_Player, _Flag);
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "RoguelikeArena_Lobby"
