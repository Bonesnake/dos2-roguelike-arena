Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION
//REGION Run these from other goals
// Adds the given integer value to the pool with the help of the string pool.
PROC
PROC_RoguelikeAdventure_IntegerPoolAdd((STRING)_Key, (INTEGER)_Value)
AND
IntegerToString(_Value, _StringValue)
THEN
PROC_RoguelikeAdventure_StringPoolAdd(_Key, _StringValue);
DB_RoguelikeAdventure_IntegerPool(_Key, _StringValue, _Value);

// Removes the given integer from the pool.
PROC
PROC_RoguelikeAdventure_IntegerPoolRemove((STRING)_Key, (INTEGER)_Value)
AND
IntegerToString(_Value, _StringValue)
THEN
PROC_RoguelikeAdventure_StringPoolRemove(_Key, _StringValue);
NOT DB_RoguelikeAdventure_IntegerPool(_Key, _StringValue, _Value);

// Forwards the call to the string pool.
PROC
PROC_RoguelikeAdventure_IntegerPoolClear((STRING)_Key)
THEN
PROC_RoguelikeAdventure_StringPoolClear(_Key);

// Clears the integer pool.
PROC
PROC_RoguelikeAdventure_IntegerPoolClear((STRING)_Key)
AND
DB_RoguelikeAdventure_IntegerPool(_Key, _StringValue, _Value)
THEN
NOT DB_RoguelikeAdventure_IntegerPool(_Key, _StringValue, _Value);

// Removes selected value.
PROC
PROC_RoguelikeAdventure_IntegerPoolClear((STRING)_Key)
AND
DB_RoguelikeAdventure_IntegerPoolSelectedValue(_Key, _Value)
THEN
NOT DB_RoguelikeAdventure_IntegerPoolSelectedValue(_Key, _Value);

// Selects a random integer from the pool with the help of the string pool. The result will be put into DB_RoguelikeAdventure_IntegerPoolSelectedValue((STRING)_Key, (INTEGER)_Value).
QRY
QRY_RoguelikeAdventure_IntegerPoolSelectRandom((STRING)_Key)
THEN
PROC_RoguelikeAdventure_IntegerPoolSelectedValueClear(_Key);
PROC_RoguelikeAdventure_IntegerPoolSelectedValuePair(_Key);

// Selects and removes a random integer from the pool.
QRY
QRY_RoguelikeAdventure_IntegerPoolSelectAndRemoveRandom((STRING)_Key)
AND
QRY_RoguelikeAdventure_IntegerPoolSelectRandom(_Key)
AND
DB_RoguelikeAdventure_IntegerPoolSelectedValue(_Key, _Value)
THEN
PROC_RoguelikeAdventure_IntegerPoolRemove(_Key, _Value);
//END_REGION

//REGION Run these from inside this goal
// Clears the selected value matching the given key.
PROC
PROC_RoguelikeAdventure_IntegerPoolSelectedValueClear((STRING)_Key)
AND
DB_RoguelikeAdventure_IntegerPoolSelectedValue(_Key, _Value)
THEN
NOT DB_RoguelikeAdventure_IntegerPoolSelectedValue(_Key, _Value);

// Pairs the selected string value with the integer value.
PROC
PROC_RoguelikeAdventure_IntegerPoolSelectedValuePair((STRING)_Key)
AND
QRY_RoguelikeAdventure_StringPoolSelectRandom(_Key)
AND
DB_RoguelikeAdventure_StringPoolSelectedValue(_Key, _StringValue)
AND
DB_RoguelikeAdventure_IntegerPool(_Key, _StringValue, _Value)
THEN
DB_RoguelikeAdventure_IntegerPoolSelectedValue(_Key, _Value);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "RoguelikeAdventure"