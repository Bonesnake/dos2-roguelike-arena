Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION
// Creates companions for the party procedurally. Most of this proc is based on GLO_LVHub_Henchmen. Detailed explanations are found there.
/*PROC
PROC_RoguelikeAdventure_CreateCompanion((STRING)_Template, (STRING)_Preset, (CHARACTERGUID)_Player, (STRING)_RecruitmentFlag)
AND
GetPosition(_Player, _X, _Y, _Z)
AND
CharacterCreateAtPosition(_X, _Y, _Z, _Template, 0, _Companion) // The character is actually created at 0, 0, 0 and is moved to the set position later (this is still needed or else the creation might fail)
AND
StringConcatenate("Hero Henchman ", _Preset, _DefaultFaction)
THEN
ObjectSetFlag(_Companion, "FTJ_RemoveSourceCollar"); // Not sure if this is actually needed
DB_GLO_PartyMembers_DefaultFaction(_Companion, _DefaultFaction); // Needed for PROC_GLO_PartyMembers_Add
DB_GLO_PartyMembers_OriginalAlignment(_Companion, "Neutral"); // Not sure about this either
CharacterMakePlayer(_Companion, _Player);
DB_RoguelikeAdventure_Companions(_Player, _Companion, _RecruitmentFlag);
TeleportTo(_Companion, _Player); // This has to be before PROC_GLO_PartyMembers_Add (unlike seen in GLO_LVHub_Henchmen, which is why henchmen are not chained to recruiter)
PROC_GLO_PartyMembers_Add(_Companion, _Player);
CharacterApplyHenchmanPreset(_Companion, _Preset);
CharacterRemoveSkill(_Companion, "Target_Bless"); // Bless is part of henchmen presets, which should only be added as a level up reward
Foop(_Companion); // Smoke effect
CharacterSetCustomName(_Companion, "Companion");*/

// Makes a character be a companion for the party. Most of this proc is based on GLO_LVHub_Henchmen. Detailed explanations are found there.
PROC
PROC_RoguelikeAdventure_MakeCompanion((CHARACTERGUID)_Character, (STRING)_TransformTemplate, (STRING)_HenchmanPreset, (CHARACTERGUID)_Recruiter)
AND
StringConcatenate("Hero Henchman ", _HenchmanPreset, _DefaultFaction)
THEN
ObjectSetFlag(_Character, "FTJ_RemoveSourceCollar"); // Not sure if this is actually needed
DB_GLO_PartyMembers_DefaultFaction(_Character, _DefaultFaction); // Needed for PROC_GLO_PartyMembers_Add
DB_GLO_PartyMembers_OriginalAlignment(_Character, "Neutral"); // Not sure about this either
CharacterMakePlayer(_Character, _Recruiter);
DB_RoguelikeAdventure_Companions(_Character, _Recruiter);
TeleportTo(_Character, _Recruiter); // This has to be before PROC_GLO_PartyMembers_Add (unlike seen in GLO_LVHub_Henchmen, which is why henchmen are not chained to recruiter)
PROC_GLO_PartyMembers_Add(_Character, _Recruiter);
Transform(_Character, _TransformTemplate, 0);
CharacterApplyHenchmanPreset(_Character, _HenchmanPreset);
CharacterRemoveSkill(_Character, "Target_Bless"); // Bless is part of henchmen presets, which should only be added as a level up reward
Foop(_Character); // Smoke effect
CharacterSetCustomName(_Character, "Companion");

// Sets up in-party dialog for companions. This specific query is required for PROC_GLO_PartyMember_Add.
QRY
QRY_GLO_PartyMembers_GetInPartyDialog((CHARACTERGUID)_Companion)
AND
DB_RoguelikeAdventure_Companions(_Companion, _)
THEN
DB_GLO_PartyMembers_InPartyDialog(_Companion, "RoguelikeAdventure_Companion");

// Manages companion dismissing. This specific proc is required.
PROC
PROC_GLO_PartyMembers_MakeNPCHook((CHARACTERGUID)_Companion)
AND
DB_RoguelikeAdventure_Companions(_Companion, _)
THEN
CharacterDisappearOutOfSight(_Companion, 0, 0, "RoguelikeAdventure_Companion_Dismissed", 0);

// This event happens after a companion has been dismissed and has disappeared out of sight.
IF
StoryEvent((CHARACTERGUID)_Companion, "RoguelikeAdventure_Companion_Dismissed")
AND
DB_RoguelikeAdventure_Companions(_Companion, _Recruiter)
THEN
PROC_GLO_PartyMembers_TransferInventoryToPlayer(_Companion, _Recruiter);
NOT DB_RoguelikeAdventure_Companions(_Companion, _Recruiter);
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "RoguelikeAdventure"