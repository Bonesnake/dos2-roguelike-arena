Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_RoguelikeArena_SourceBeaconTemplate("RoguelikeArena_SourceBeacon_e5a92070-79ab-4ac6-b521-f8837bc16b46");
KBSECTION
PROC
PROC_RoguelikeArena_SpawnSourceBeacons((INTEGER)_Arena, (INTEGER)_Level, (INTEGER)_Difficulty)
AND
QRY_RoguelikeArena_RandomCount(_Difficulty, 1, 3, 1)
AND
DB_RoguelikeArena_RandomResult(_SourceBeaconCount)
THEN
PROC_RoguelikeArena_SpawnSourceBeacons(_Arena, _Level, _Difficulty, _SourceBeaconCount);

PROC
PROC_RoguelikeArena_SpawnSourceBeacons((INTEGER)_Arena, (INTEGER)_Level, (INTEGER)_Difficulty, (INTEGER)_Count)
AND
QRY_RoguelikeArena_CreateIndices(_Count)
AND
DB_RoguelikeArena_CreatedIndices(_)
THEN
PROC_RoguelikeArena_SpawnSourceBeacon(_Arena, _Level, _Difficulty);

PROC
PROC_RoguelikeArena_SpawnSourceBeacon((INTEGER)_Arena, (INTEGER)_Level, (INTEGER)_Difficulty)
AND
DB_RoguelikeArena_SourceBeaconTemplate(_Template)
AND
QRY_RoguelikeArena_GetRandomPositionInArena(_Arena, "SPAWNER_EVENT")
AND
DB_RoguelikeArena_RandomPositionResult(_X, _Y, _Z)
AND
CreateItemTemplateAtPosition(_Template, _X, _Y, _Z, _SourceBeacon)
THEN
PROC_RoguelikeArena_ItemRandomRotate(_SourceBeacon);
// PROC_RoguelikeArena_ItemTeleportToValidPosition(_SourceBeacon);
// PROC_RoguelikeArena_ItemSnapToGround(_SourceBeacon);
ItemLevelUpTo(_SourceBeacon, _Level);
PROC_RoguelikeArena_RegisterForCleanup(_SourceBeacon, _Arena);
SetInArena(_SourceBeacon, 1);
DB_RoguelikeArena_SourceBeacons(_Arena, _SourceBeacon);

// Make sure beacons don't remain hostile after combat ends.
PROC
PROC_RoguelikeArena_ArenaCombatEnded(_Arena, _, _, _)
AND
DB_RoguelikeArena_SourceBeacons(_Arena, _SourceBeacon)
AND
QRY_RoguelikeArena_ObjectIsInArena(_SourceBeacon, _Arena)
AND
IsInArena(_SourceBeacon, 1)
THEN
SetInArena(_SourceBeacon, 0);
SetCanJoinCombat(_SourceBeacon, 0);
SetCanFight(_SourceBeacon, 0);
NOT DB_RoguelikeArena_SourceBeacons(_Arena, _SourceBeacon);
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "RoguelikeArena_Arena_Event"
