Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_RoguelikeArena_GlobalMusicHelperOutside(ITEMGUID_MusicHelper_Outside_9b049514-edc0-4c94-8544-ed4d6c1fe42c);

DB_RoguelikeArena_MusicTriggers(TRIGGERGUID_Music01_3e009c68-8cfc-4c07-b016-2324ce591b8c);
DB_RoguelikeArena_MusicTriggers(TRIGGERGUID_Music02_23dacc8e-c274-49df-898e-2618737ef2dc);
DB_RoguelikeArena_MusicTriggers(TRIGGERGUID_Music03_824fd5cd-ed63-48f9-afe4-3383f1fd48bc);
DB_RoguelikeArena_MusicTriggers(TRIGGERGUID_Music04_edf644e9-ccbb-45ae-bf2e-030d1e3b0f8c);
DB_RoguelikeArena_MusicTriggers(TRIGGERGUID_Music05_992ac2db-0ef4-46e3-b95a-40cc935be2d0);

DB_RoguelikeArena_BossMusicTriggers(TRIGGERGUID_BossMusic01_58190241-8aae-4746-9f83-c738111e7204);
DB_RoguelikeArena_BossMusicTriggers(TRIGGERGUID_BossMusic02_e26f6263-a4d3-49d6-9c2c-0b9d0fbf2244);
DB_RoguelikeArena_BossMusicTriggers(TRIGGERGUID_BossMusic03_c068ebcc-4fa2-49e5-b57c-f66560fdda3a);

PROC_RoguelikeArena_GuidStringAddMusicTriggers();
PROC_RoguelikeArena_GuidStringAddBossMusicTriggers();
KBSECTION
// Restores music triggers after loading a save. This is necessary, because music triggers' position is not saved.
IF
SavegameLoaded(_, _, _, _)
AND
DB_RoguelikeArena_ActiveMusicTriggers(_Region, _Trigger)
THEN
PROC_RoguelikeArena_MoveInMusicTrigger(_Region, _Trigger);

IF
DB_RoguelikeArena_ActiveRegions(_Region, _, _Index)
THEN
PROC_RoguelikeArena_PlayRandomMusic(_Region, _Index);

IF
DB_RoguelikeArena_ActiveRegionsHelper(_Region, _Level, _Index)
AND
NOT DB_RoguelikeArena_ActiveRegions(_Region, _Level, _Index)
THEN
PROC_RoguelikeArena_StopMusic(_Region);

// Plays a random music according to the current progression.
PROC
PROC_RoguelikeArena_PlayRandomMusic((INTEGER)_Region, (INTEGER)_Index)
AND
DB_RoguelikeArena_Singleton_RegionsPerLevel(_RegionsPerLevel)
AND
_Index < _RegionsPerLevel
AND
QRY_RoguelikeArena_GuidStringPoolSelectAndRemoveRandom("MusicTriggerPool")
AND
DB_RoguelikeArena_GuidStringPoolSelectedValue("MusicTriggerPool", _Trigger)
THEN
PROC_RoguelikeArena_MoveInMusicTrigger(_Region, _Trigger);

// Plays a random music according to the current progression.
PROC
PROC_RoguelikeArena_PlayRandomMusic(_Region, _Index)
AND
DB_RoguelikeArena_Singleton_RegionsPerLevel(_RegionsPerLevel)
AND
_Index >= _RegionsPerLevel
AND
QRY_RoguelikeArena_GuidStringPoolSelectAndRemoveRandom("BossMusicTriggerPool")
AND
DB_RoguelikeArena_GuidStringPoolSelectedValue("BossMusicTriggerPool", _Trigger)
THEN
PROC_RoguelikeArena_MoveInMusicTrigger(_Region, _Trigger);

// Removes music trigger from the region.
PROC
PROC_RoguelikeArena_StopMusic((INTEGER)_Region)
AND
DB_RoguelikeArena_ActiveMusicTriggers(_Region, _Trigger)
AND
DB_RoguelikeArena_MusicTriggers(_Trigger)
THEN
PROC_RoguelikeArena_GuidStringPoolAdd("MusicTriggerPool", _Trigger);
PROC_RoguelikeArena_MoveOutMusicTrigger(_Trigger);

// Removes music trigger from the region.
PROC
PROC_RoguelikeArena_StopMusic((INTEGER)_Region)
AND
DB_RoguelikeArena_ActiveMusicTriggers(_Region, _Trigger)
AND
DB_RoguelikeArena_BossMusicTriggers(_Trigger)
THEN
PROC_RoguelikeArena_GuidStringPoolAdd("BossMusicTriggerPool", _Trigger);
PROC_RoguelikeArena_MoveOutMusicTrigger(_Trigger);

// Moves in a music trigger to the given region and stores it in a DB.
PROC
PROC_RoguelikeArena_MoveInMusicTrigger((INTEGER)_Region, (GUIDSTRING)_Trigger)
AND
DB_RoguelikeArena_Regions(_Region, _Target)
THEN
TeleportTo(_Trigger, _Target, "", 0);

// Moves in a music trigger to the given region and stores it in a DB.
PROC
PROC_RoguelikeArena_MoveInMusicTrigger((INTEGER)_Region, (GUIDSTRING)_Trigger)
AND
NOT DB_RoguelikeArena_ActiveMusicTriggers(_Region, _Trigger)
THEN
DB_RoguelikeArena_ActiveMusicTriggers(_Region, _Trigger);

// Moves out a music trigger and removes it from the DB.
PROC
PROC_RoguelikeArena_MoveOutMusicTrigger((GUIDSTRING)_Trigger)
AND
DB_RoguelikeArena_GlobalMusicHelperOutside(_Target)
THEN
TeleportTo(_Trigger, _Target, "", 0);

// Moves out a music trigger and removes it from the DB.
PROC
PROC_RoguelikeArena_MoveOutMusicTrigger((GUIDSTRING)_Trigger)
AND
DB_RoguelikeArena_ActiveMusicTriggers(_Region, _Trigger)
THEN
NOT DB_RoguelikeArena_ActiveMusicTriggers(_Region, _Trigger);

PROC
PROC_RoguelikeArena_GuidStringAddMusicTriggers()
AND
DB_RoguelikeArena_MusicTriggers(_Trigger)
THEN
PROC_RoguelikeArena_GuidStringPoolAdd("MusicTriggerPool", _Trigger);

PROC
PROC_RoguelikeArena_GuidStringAddBossMusicTriggers()
AND
DB_RoguelikeArena_BossMusicTriggers(_Trigger)
THEN
PROC_RoguelikeArena_GuidStringPoolAdd("BossMusicTriggerPool", _Trigger);
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "RoguelikeArena_Region"
