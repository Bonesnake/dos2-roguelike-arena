Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION
PROC
PROC_RoguelikeArena_GenerateEncounters((INTEGER)_Region)
AND
DB_RoguelikeArena_Sections(_Region, _Section, _)
AND
QRY_RoguelikeArena_IsSectionOpen(_Region, _Section)
THEN
PROC_RoguelikeArena_GenerateRandomEncounter(_Region, _Section);

PROC
PROC_RoguelikeArena_GenerateRandomEncounter((INTEGER)_Region, (INTEGER)_Section)
AND
NOT QRY_RoguelikeArena_IsSectionBackwardsExit(_Region, _Section)
AND
NOT QRY_RoguelikeArena_IsSectionForwardExit(_Region, _Section)
AND
QRY_RoguelikeArena_RandomInclusive(2, 4)
AND
DB_Singleton("RoguelikeArena_RandomResult", _Encounter)
THEN
PROC_RoguelikeArena_GenerateEncounter(_Region, _Section, _Encounter);

PROC
PROC_RoguelikeArena_GenerateRandomEncounter((INTEGER)_Region, (INTEGER)_Section)
AND
QRY_RoguelikeArena_IsSectionForwardExit(_Region, _Section)
THEN
PROC_RoguelikeArena_GenerateEncounter(_Region, _Section, 1);
PROC_RoguelikeArena_GenerateEncounter(_Region, _Section, 2);

PROC
PROC_RoguelikeArena_GenerateEncounter((INTEGER)_Region, (INTEGER)_Section, (INTEGER)_Encounter)
AND
_Encounter == 1
AND
DB_RoguelikeArena_ActiveRegions(_Region, _Level, _Index)
THEN
PROC_RoguelikeArena_SpawnEnemies(_Region, _Section, _Level, _Index);

PROC
PROC_RoguelikeArena_GenerateEncounter((INTEGER)_Region, (INTEGER)_Section, (INTEGER)_Encounter)
AND
_Encounter == 2
AND
DB_RoguelikeArena_ActiveRegions(_Region, _Level, _Index)
THEN
PROC_RoguelikeArena_SpawnChest(_Region, _Section, _Level, _Index);

PROC
PROC_RoguelikeArena_GenerateEncounter((INTEGER)_Region, (INTEGER)_Section, (INTEGER)_Encounter)
AND
_Encounter == 3
AND
DB_RoguelikeArena_ActiveRegions(_Region, _Level, _Index)
THEN
PROC_RoguelikeArena_SpawnContainers(_Region, _Section, _Level, _Index);

PROC
PROC_RoguelikeArena_GenerateEncounter((INTEGER)_Region, (INTEGER)_Section, (INTEGER)_Encounter)
AND
_Encounter == 4
AND
DB_RoguelikeArena_ActiveRegions(_Region, _Level, _Index)
THEN
PROC_RoguelikeArena_SpawnHazards(_Region, _Section, _Level);

// PROC
// PROC_RoguelikeArena_GenerateEncounter((INTEGER)_Region, (INTEGER)_Section, (INTEGER)_Encounter)
// AND
// _Encounter == 5
// AND
// DB_RoguelikeArena_ActiveRegions(_Region, _Level, _Index)
// THEN
// PROC_RoguelikeArena_SpawnTreasures(_Region, _Section, _Level, _Index);
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "RoguelikeArena_Region_Encounter"
